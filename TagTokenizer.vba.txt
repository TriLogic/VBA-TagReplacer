VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "TagTokenizer"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Compare Database
Option Explicit

Public Buffer As String
Public Index As Integer

Public Sub Setup(Source As String)
    Call Clear
    Buffer = Source
End Sub

Public Sub Clear()
    Index = 1
End Sub

Public Function GetToken(ByRef Tkn As TagToken) As Boolean
    
    Dim Tkc As String
    
    Set Tkn = New TagToken
    Tkn.TkType = TkNUL
    
    Do While Index <= Len(Buffer)
    
        Tkc = Mid(Buffer, Index, 1)
        Select Case (Tkc)
        Case "$"
                
            ' Is there room for a TkLHS Tag?
            If Len(Buffer) > Index Then
            
                ' Is it a TkLHS Tag?
                If Mid(Buffer, Index + 1, 1) = "{" Then
                
                    ' If previous tag already started return it.
                    If Tkn.TkIndex > 0 Then
                        GetToken = True
                        Exit Function
                    End If
                
                    ' No previous so return a TkLHS
                    Tkn.TkType = TkLHS
                    Tkn.TkValue = "${"
                    Tkn.TkIndex = Index
                    
                    Index = Index + 2
                    GetToken = True
                    Exit Function
                    
                End If
                
            End If
            
            ' Otherwise treat it like a TkTXT char
        
        Case "}"
        
            ' If previous tag already started return it.
            If Tkn.TkIndex > 0 Then
                GetToken = True
                Exit Function
            End If
            
            ' Otherwise return a TkRHS Tag
            Tkn.TkIndex = Index
            Tkn.TkType = TkRHS
            Tkn.TkValue = "}"
            
            Index = Index + 1
            GetToken = True
            Exit Function
        
        End Select
        
        If Tkn.TkIndex = 0 Then
            Tkn.TkIndex = Index
            Tkn.TkType = TkTXT
        End If
        
        Tkn.TkValue = Tkn.TkValue & Tkc
        Index = Index + 1
    
    Loop
    
    GetToken = (Tkn.TkIndex > 0)

End Function
